extends Node3D
class_name GridAnchor

@export var direction: CardinalDirections.CardinalDirection = CardinalDirections.CardinalDirection.DOWN

@export var required_transportation_mode: TransportationMode

## If an entity cannot anchor, does it mean it should pass through the anchor.
## Example if the anchor is the down (or up) direction and is a water surface
## and the entity cannot swim, it should sink through the anchor.
@export var pass_through_on_refuse: bool

## If it is possible to pass through the anchor into the node
@export var pass_through_reverse: bool

## For example a wall mounted ladder will have an inherent down with the direction of gravity in most cases
@export var inherrent_axis_down: CardinalDirections.CardinalDirection = CardinalDirections.CardinalDirection.NONE

func calculate_anchor_down(gravity: CardinalDirections.CardinalDirection, entity_down: CardinalDirections.CardinalDirection) -> CardinalDirections.CardinalDirection:
    if inherrent_axis_down == CardinalDirections.CardinalDirection.NONE || inherrent_axis_down == direction:
        return direction
    elif CardinalDirections.is_parallell(gravity, inherrent_axis_down):
        return gravity
    elif CardinalDirections.is_parallell(entity_down, inherrent_axis_down):
        return entity_down
    return inherrent_axis_down

var disabled: bool:
    set(value):
        if !disabled && value:
            for entity: GridEntity in get_grid_node().get_level().grid_entities:
                if entity == null || !is_instance_valid(entity) || !entity.is_inside_tree():
                    continue

                if entity.anchor == self:
                    var push_direction: CardinalDirections.CardinalDirection = calculate_anchor_down(entity.get_level().gravity, entity.down)
                    if push_direction == CardinalDirections.CardinalDirection.NONE || push_direction == direction:
                        if CardinalDirections.is_parallell(entity.look_direction, direction):
                            push_direction = CardinalDirections.random_orthogonal(direction)
                        else:
                            push_direction = entity.look_direction

                    var movement: Movement.MovementType = Movement.from_directions(push_direction, entity.look_direction, entity.down)
                    if !entity.force_movement(movement):
                        push_warning("[Grid Anchor %s] Anchor became disabled and could not get rid of attached entity %s by movement %s" % [self, entity, Movement.name(movement)])
        disabled = value

var _node_side: GridNodeSide

func get_node_side() -> GridNodeSide:
    if _node_side == null:
        _node_side = GridNodeSide.find_node_side_parent(self)
    return _node_side

func get_grid_node() -> GridNode:
    var side: GridNodeSide = get_node_side()
    if side == null:
        return null
    return side.get_grid_node(self)

var coordinates: Vector3i:
    get():
        var node: GridNode = get_grid_node()
        if node == null:
            push_error("Grid Anchor %s not part of any grid node" % self)
            return Vector3i.ZERO
        return node.coordinates

func _ready() -> void:
    var node_side: GridNodeSide = get_node_side()
    if node_side == null:
        push_error("%s doesn't have a GridNodeSide parent" % name)
    elif !CardinalDirections.is_parallell(direction, node_side.direction):
        # TODO: Something is fishy here
        GridNodeSide.set_direction_from_rotation(node_side)
        # push_error("%s's direction %s isn't parallell to the GridNodeSide direction %s" % [name, direction, node_side.direction])

    # _draw_debug_edges()

func _draw_debug_edges() -> void:
    for edge: CardinalDirections.CardinalDirection in CardinalDirections.orthogonals(direction):
        _draw_debug_sphere(get_edge_position(edge, false), 0.1)

func can_anchor(entity: GridEntity) -> bool:
    return (
        required_transportation_mode.supports(entity.transportation_abilities) &&
        !get_grid_node().any_event_blocks_anchorage(entity, direction)
    )

func get_edge_position(edge_direction: CardinalDirections.CardinalDirection, local: bool = false) -> Vector3:
    var node: GridNode = get_grid_node()
    if node == null:
        return global_position

    if direction == edge_direction || CardinalDirections.invert(direction) == edge_direction:
        push_warning("%s is anchor %s, it doesn't have an edge %s, using it's center" % [
            name,
            CardinalDirections.name(direction),
            CardinalDirections.name(edge_direction)])
        return global_position

    var offset: Vector3 = node.get_level().node_size * 0.5 * Vector3(CardinalDirections.direction_to_vectori(edge_direction))

    if local:
        return offset

    return global_position + offset

func _draw_debug_sphere(location: Vector3, size: float) -> void:
    # Create sphere with low detail of size.
    var sphere: SphereMesh = SphereMesh.new()
    sphere.radial_segments = 4
    sphere.rings = 4
    sphere.radius = size
    sphere.height = size * 2
    # Bright red material (unshaded).
    var material: StandardMaterial3D = StandardMaterial3D.new()
    material.albedo_color = Color(1, 0, 0, 0.5)
    material.shading_mode = BaseMaterial3D.SHADING_MODE_UNSHADED
    sphere.surface_set_material(0, material)

    # Add to meshinstance in the right place.
    var node: MeshInstance3D = MeshInstance3D.new()
    add_child(node)
    node.mesh = sphere
    node.global_position = location

static func find_anchor_parent(current: Node, inclusive: bool = true) ->  GridAnchor:
    if inclusive && current is GridAnchor:
        return current as GridAnchor

    var parent: Node = current.get_parent()

    if parent == null:
        return null

    if parent is GridAnchor:
        return parent as GridAnchor

    return find_anchor_parent(parent, false)

static func summarize(grid_anchor: GridAnchor) -> String:
    if grid_anchor == null:
        return "/NO ANCHOR/"

    return "%s side %s of %s" % [grid_anchor, CardinalDirections.name(grid_anchor.direction), grid_anchor.coordinates]
