@tool
extends Panel
class_name GridLevelDiggerPanel

signal on_update_raw_selection(nodes: Array[Node])
signal on_update_selected_nodes(nodes: Array[GridNode])
signal on_update_level(level: GridLevelCore)

@export var minimize_icon: Texture2D
@export var maximize_icon: Texture2D
@export var minmax_btn: TextureButton
@export var body: Control

var level: GridLevelCore:
    set(value):
        if inside_level:
            if level != value:
                material_overrides = null

            level = value

            on_update_level.emit(level if inside_level else null)
        else:
            if level != value:
                material_overrides = null

            level = value

var material_overrides: LevelMaterialOverrides:
    get():
        var _level: GridLevelCore = level
        if _level == null:
            return null
        if material_overrides == null:
            material_overrides = ArrayUtils.first_or_default(_level.find_children("", "LevelMaterialOverrides"))
        return material_overrides

var preview_style_targets: bool:
    get():
        return DictionaryUtils.safe_getb(
            DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false),
            _PREVIEW_STYLE_TARGETS_KEY,
            false,
            false,
        )

    set(value):
        var general: Dictionary = DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false)
        general[_PREVIEW_STYLE_TARGETS_KEY] = value
        _stored_settings[_GENERAL_KEY] = general
        if !settings_storage.store_data(0, _stored_settings):
            push_warning("Failed to store updated preview style targets value")

var digout_size: Vector3i:
    get():
        return DictionaryUtils.safe_getv3i(
            DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false),
            _DIGOUT_SIZE,
            Vector3i(3, 1, 3),
            false,
        ).maxi(1)

var digout_preserve: bool:
    get():
        return DictionaryUtils.safe_getb(
            DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false),
            _DIGOUT_PRESERVE,
            false,
            false,
        )

func set_digout_settings(new_size: Vector3i, preserve: bool) -> void:
        var general: Dictionary = DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false)
        general[_DIGOUT_PRESERVE] = preserve
        general[_DIGOUT_SIZE] = new_size.maxi(1)
        _stored_settings[_GENERAL_KEY] = general
        if !settings_storage.store_data(0, _stored_settings):
            push_warning("Failed to store digout setting")

var boxin_size: Vector3i:
    get():
        return DictionaryUtils.safe_getv3i(
            DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false),
            _BOXIN_SIZE,
            Vector3i(12, 12, 12),
            false,
        ).maxi(1)

var boxin_preserve: bool:
    get():
        return DictionaryUtils.safe_getb(
            DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false),
            _BOXIN_PRESERVE,
            false,
            false,
        )

func set_boxin_settings(new_size: Vector3i, preserve: bool) -> void:
        var general: Dictionary = DictionaryUtils.safe_getd(_stored_settings, _GENERAL_KEY, {}, false)
        general[_BOXIN_PRESERVE] = preserve
        general[_BOXIN_SIZE] = new_size.maxi(1)
        _stored_settings[_GENERAL_KEY] = general
        if !settings_storage.store_data(0, _stored_settings):
            push_warning("Failed to store new box in setting")

var _node: GridNode
var _anchor: GridAnchor

var inside_level: bool:
    set(value):
        if value != inside_level:
            inside_level = value
            on_update_level.emit(level if inside_level else null)

var coordinates: Vector3i = Vector3i.ZERO : set = _set_coords
func _set_coords(value: Vector3i) -> void:
    print_debug("[GLD Panel] %s -> %s" % [coordinates, value])
    coordinates = value
    var node_idx: int = all_level_nodes.find_custom(func (n: GridNode) -> bool: return n.coordinates == value)
    _node = all_level_nodes[node_idx] if node_idx >= 0 else null
    _anchor = null

    _draw_debug_node_meshes()
    _draw_debug_arrow()

var undo_redo: EditorUndoRedoManager

var _edited_scene_getter: Variant
var edited_scene_root: Node:
    get():
        if _edited_scene_getter is Callable:
            var getter: Callable = _edited_scene_getter
            return getter.call()
        return null

var all_level_nodes: Array[GridNode] = []
var _debug_arrow_mesh: MeshInstance3D

@export var tab_container: TabContainer

@export var about_tab: Control
@export var level_tab: Control
@export var digging_tab: Control
@export var manipulate_tab: Control
@export var style_tab: Control

@export var styles: GridLevelStyle
@export var node_digger: GridNodeDigger
@export var level_actions: GridLevelActions
@export var manipulator: GridLevelManipulator
@export var zones: GridLevelZoner
@export var update_nav: GridLevelNav

@export var settings_storage: SaveStorageProvider

var look_direction: CardinalDirections.CardinalDirection = CardinalDirections.CardinalDirection.NORTH

var selected_nodes: Array[GridNode]:
    set(value):
        selected_nodes = value
        on_update_selected_nodes.emit(value)

var raw_selection: Array[Node]:
    set(value):
        raw_selection = value
        on_update_raw_selection.emit(value)

func _enter_tree() -> void:
    _load_settings()
    if styles.on_style_updated.connect(_handle_style_updated) != OK:
        push_error("Failed to connect style updated")

    register_nav(node_digger.nav)
    register_nav(update_nav)

var _navs: Array[GridLevelNav]

func _exit_tree() -> void:
    _remove_debug_arrow()

    styles.on_style_updated.disconnect(_handle_style_updated)

    for nav: GridLevelNav in _navs:
        nav.on_update_nav.disconnect(_handle_update_nav)
    _navs.clear()
    
func _ready() -> void:
    _on_texture_button_toggled(minmax_btn.button_pressed)
    
func register_nav(nav: GridLevelNav) -> void:
    if nav.on_update_nav.connect(_handle_update_nav) != OK:
        push_error("Failed to connect update nav")
        _navs.append(nav)

func unregister_nav(nav: GridLevelNav) -> void:
    if _navs.has(nav):
        nav.on_update_nav.disconnect(_handle_update_nav)
        _navs.erase(nav)

func _handle_update_nav(coords: Vector3i, direction: CardinalDirections.CardinalDirection) -> void:
    coordinates = coords
    look_direction = direction
    _draw_debug_arrow()

func get_level() -> GridLevelCore:
    return level

func get_grid_node() -> GridNode:
    return _node

func get_grid_node_at(node_coords: Vector3i) -> GridNode:
    var idx: int = all_level_nodes.find_custom(func (n: GridNode) -> bool: return n.coordinates == node_coords)
    if idx < 0:
        return null
    return all_level_nodes[idx]

## The node at current coordinates
func get_focus_node() -> GridNode:
    return get_grid_node_at(coordinates)

func add_grid_node(node: GridNode) -> void:
    if all_level_nodes.has(node):
        return

    all_level_nodes.append(node)

func remove_grid_node(node: GridNode) -> void:
    all_level_nodes.erase(node)

func get_grid_anchor() -> GridAnchor:
    return _anchor

func set_level(new_level: GridLevelCore) -> void:
    if level != new_level:
        level = new_level
    _node = null
    _anchor = null
    inside_level = true
    coordinates = Vector3i.ZERO

    refresh_level_nodes()

    sync_ui()

func refresh_level_nodes() -> void:
    all_level_nodes.clear()

    if level != null:
        all_level_nodes.append_array(level.find_children("", "GridNode"))


func _update_level_if_needed(grid_node: GridNode) -> bool:
    var new_level: GridLevelCore = GridLevelCore.find_level_parent(grid_node)
    if level != new_level:
        level = new_level

        refresh_level_nodes()
        return true

    elif all_level_nodes.size() == 0:
        all_level_nodes.append_array(new_level.find_children("", "GridNode"))

    return false

func set_grid_node(grid_node: GridNode) -> void:
    if grid_node == _node:
        @warning_ignore_start("return_value_discarded")
        _update_level_if_needed(grid_node)
        @warning_ignore_restore("return_value_discarded")
        if !inside_level:
            inside_level = true
            sync_ui()
        return

    if !_update_level_if_needed(grid_node) && !all_level_nodes.has(grid_node):
        if all_level_nodes.size() == 0:
            refresh_level_nodes()
        else:
            all_level_nodes.append(grid_node)

    _node = grid_node
    _anchor = null
    inside_level = true

    coordinates = grid_node.coordinates

    sync_ui()

func set_grid_anchor(grid_anchor: GridAnchor) -> void:
    if _anchor == grid_anchor:
        if !inside_level:
            inside_level = true
            sync_ui()
        return

    var grid_node: GridNode = GridNode.find_node_parent(grid_anchor)
    if grid_node != _node:
        if !_update_level_if_needed(grid_node) && !all_level_nodes.has(grid_node):
            if all_level_nodes.size() == 0:
                refresh_level_nodes()
            else:
                all_level_nodes.append(grid_node)

        _node = grid_node
        if _node != null:
            coordinates = _node.coordinates

    inside_level = true
    sync_ui()

func set_not_selected_level() -> void:
    inside_level = false
    sync_ui()

func get_tab_index(control: Control) -> int:
    return control.get_parent().get_children().find(control)

func sync_ui() -> void:
    if inside_level:
        tab_container.set_tab_disabled(get_tab_index(level_tab), false)
        tab_container.set_tab_disabled(get_tab_index(digging_tab), false)
        tab_container.set_tab_disabled(get_tab_index(manipulate_tab), false)


        if tab_container.current_tab == get_tab_index(about_tab):
            tab_container.current_tab = get_tab_index(level_tab)

        level_actions.sync_ui()
        manipulator.sync()
        node_digger.sync()

        _draw_debug_node_meshes()
    else:
        tab_container.set_tab_disabled(get_tab_index(level_tab), true)
        tab_container.set_tab_disabled(get_tab_index(digging_tab), true)
        tab_container.set_tab_disabled(get_tab_index(manipulate_tab), true)

        tab_container.current_tab = get_tab_index(about_tab)

        remove_debug_nodes()

var _node_debug_mesh: MeshInstance3D
var _node_debug_center: MeshInstance3D
var _node_debug_anchors: Array[MeshInstance3D] = []

func _draw_debug_node_meshes() -> void:
    _clear_node_debug_frame()
    _clear_node_debug_center()
    _clear_node_debug_anchors()

    if level != null:
        var center: Vector3 = GridLevelCore.node_center(level, coordinates)

        _node_debug_mesh = DebugDraw.box(
            level,
            center,
            level.node_size,
            Color.MAGENTA)

        var node: GridNode = get_grid_node_at(coordinates)

        if node != null:
            _node_debug_center = DebugDraw.sphere(level, center, DebugDraw.direction_to_color(CardinalDirections.CardinalDirection.NONE))

            for node_side: GridNodeSide in node.find_children("", "GridNodeSide"):
                var anchor: GridAnchor = node_side.anchor
                if anchor != null && anchor.required_transportation_mode.mode != TransportationMode.NONE:
                    _node_debug_anchors.append(
                        DebugDraw.sphere(node, anchor.global_position, DebugDraw.direction_to_color(node_side.direction), 0.1)
                    )

func _clear_node_debug_frame() -> void:
    if _node_debug_mesh != null:
        _node_debug_mesh.queue_free()
        _node_debug_mesh = null

func _clear_node_debug_center() -> void:
    if _node_debug_center != null:
        _node_debug_center.queue_free()
        _node_debug_center = null

func _clear_node_debug_anchors() -> void:
    if _node_debug_anchors.is_empty():
        return

    for mesh: MeshInstance3D in _node_debug_anchors:
        if mesh == null:
            continue
        mesh.queue_free()

    _node_debug_anchors.clear()

func remove_debug_nodes() -> void:
    _clear_node_debug_frame()
    _clear_node_debug_center()
    _clear_node_debug_anchors()
    _remove_debug_arrow()

var _stored_settings: Dictionary
const _STYLE_KEY: String = "style"
const _GENERAL_KEY: String = "general"
const _PREVIEW_STYLE_TARGETS_KEY: String = "preview-style-targeets"
const _DIGOUT_SIZE: String = "digout-size"
const _DIGOUT_PRESERVE: String = "digout-preserve"
const _BOXIN_SIZE: String = "boxin-size"
const _BOXIN_PRESERVE: String = "boxin-preserve"

func _handle_style_updated() -> void:
    _stored_settings[_STYLE_KEY] = styles.collect_save_data()
    if !settings_storage.store_data(0, _stored_settings):
        push_warning("Failed to update style")

func _load_settings() -> void:
    _stored_settings = settings_storage.retrieve_data(0, true)
    styles.load_from_save(DictionaryUtils.safe_getd(_stored_settings, _STYLE_KEY, {}, false))

func _draw_debug_arrow() -> void:
    _remove_debug_arrow()

    var center: Vector3 = GridLevelCore.node_center(level, coordinates)
    var target: Vector3 = center + CardinalDirections.direction_to_vector(look_direction) * 0.75

    _debug_arrow_mesh = DebugDraw.arrow(
        level,
        center,
        target,
        Color.MAGENTA,
    )

func _remove_debug_arrow() -> void:
    if _debug_arrow_mesh != null:
        _debug_arrow_mesh.queue_free()
        _debug_arrow_mesh = null


func _on_texture_button_toggled(toggled_on: bool) -> void:
    if toggled_on:
        custom_minimum_size = Vector2(0, 500)
        minmax_btn.texture_normal = minimize_icon
        body.show()
    else:
        custom_minimum_size = Vector2(0, 20)
        minmax_btn.texture_normal = maximize_icon
        body.hide()
