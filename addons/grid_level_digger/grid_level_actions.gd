@tool
extends VBoxContainer
class_name GridLevelActions

signal on_organize_nodes(by_elevation: bool)

@export
var panel: GridLevelDiggerPanel

@export
var style: GridLevelStyle

@export
var info: Label

@export
var organize_btn: Button

var organize_by_elevation: bool = true

func _on_align_all_nodes_pressed() -> void:
    var changed: bool = false
    panel.undo_redo.create_action("GridLevelAction: Sync all node positions")

    for node: GridNode in panel.all_level_nodes:
        var new_position: Vector3 = GridLevelCore.node_position_from_coordinates(panel.level, node.coordinates)

        if node.global_position != new_position:
            panel.undo_redo.add_do_property(node, "global_position", new_position)
            panel.undo_redo.add_undo_property(node, "global_position", node.global_position)
            changed = true

    panel.undo_redo.commit_action()
    if changed:
        EditorInterface.mark_scene_as_unsaved()

func _on_set_all_wall_rotations_pressed() -> void:
    for node: GridNode in panel.all_level_nodes:
        for node_side: GridNodeSide in node.find_children("", "GridNodeSide"):
            GridNodeSide.set_direction_from_rotation(node_side)

    panel.sync_ui()

func _on_refresh_level_nodes_pressed() -> void:
    panel.refresh_level_nodes()

func sync_ui() -> void:
    info.text = "Level: %s nodes" % panel.all_level_nodes.size()

func _on_organize_nodes_button_pressed() -> void:
    if panel.level == null:
        push_warning("No level selected")
        return
    organize_level()

func _on_organize_nodes_by_elevation_toggled(toggled_on:bool) -> void:
    organize_by_elevation = toggled_on
    organize_btn.disabled = !toggled_on
    on_organize_nodes.emit(organize_by_elevation)

const _ELEVATION_NODE_PATTERN: String = "[Ee]levation (?<elevation>-?\\d+)"
const _NEW_ELEVATION_NODE_NAME_PATTERN: String = "Elevation %s"

func organize_level() -> void:
    var geometry_root: Node3D = GridLevelCore.get_level_geometry_root(panel.level)
    var all_nodes: Array[GridNode] = panel.all_level_nodes
    var level_elevation_children: Dictionary[int, Node3D] = {}

    var pattern: RegEx = RegEx.new()
    if pattern.compile(_ELEVATION_NODE_PATTERN) != OK:
        push_error("Failed to compile elevation pattern")

    for child: Node in geometry_root.get_children():
        if child is not Node3D or child is GridNode:
            continue

        var result: RegExMatch = pattern.search(child.name)
        if not result:
            continue

        var elevation: int = int(result.get_string("elevation"))
        if level_elevation_children.has(elevation):
            push_warning("Duplicate elevation nodes %s and %s both act for elevation %s" % [level_elevation_children[elevation].name, child.name, elevation])
            continue

        level_elevation_children[elevation] = child as Node3D

    for node: GridNode in all_nodes:
        var node_elevation: int = node.coordinates.y
        if !level_elevation_children.has(node_elevation):
            var new_elevation_node: Node3D = Node3D.new()
            new_elevation_node.name = _NEW_ELEVATION_NODE_NAME_PATTERN % node_elevation

            geometry_root.add_child(new_elevation_node)
            new_elevation_node.owner = geometry_root.get_tree().edited_scene_root

            level_elevation_children[node_elevation] = new_elevation_node

        var elevation_node: Node3D = level_elevation_children[node_elevation]
        if node.get_parent() == elevation_node:
            continue

        node.reparent(elevation_node, true)

    var elevations: Array[int] = level_elevation_children.keys()
    elevations.sort()

    var child_idx: int = 0
    for elevation: int in elevations:
        var enode: Node3D = level_elevation_children[elevation]
        geometry_root.move_child(enode, child_idx)
        child_idx += 1

    EditorInterface.mark_scene_as_unsaved()

static func get_or_add_elevation_parent(level: GridLevelCore, elevation: int) -> Node3D:
    var geometry_root: Node3D = GridLevelCore.get_level_geometry_root(level)

    var pattern: RegEx = RegEx.new()
    if pattern.compile(_ELEVATION_NODE_PATTERN) != OK:
        push_error("Failed to compile elevation pattern")

    for child: Node in geometry_root.get_children():
        if child is not Node3D or child is GridNode:
            continue

        var result: RegExMatch = pattern.search(child.name)
        if not result:
            continue

        var child_elevation: int = int(result.get_string("elevation"))

        if child_elevation == elevation:
            return child

    var new_elevation_node: Node3D = Node3D.new()
    new_elevation_node.name = _NEW_ELEVATION_NODE_NAME_PATTERN % elevation

    geometry_root.add_child(new_elevation_node)
    new_elevation_node.owner = geometry_root.get_tree().edited_scene_root

    EditorInterface.mark_scene_as_unsaved()

    return new_elevation_node

var _ceiling_layer: int = 2

func _on_hide_ceiling_layer_toggled(toggled_on:bool) -> void:
    var view: SubViewport = EditorInterface.get_editor_viewport_3d(0)
    var cam: Camera3D = view.get_camera_3d()
    cam.set_cull_mask_value(_ceiling_layer, !toggled_on)

func _on_ceiling_layer_value_changed(value:float) -> void:
    _ceiling_layer = roundi(value)

func _on_position_all_enemies_pressed() -> void:
    var level: GridLevelCore = panel.level
    var unsaved: bool
    if level == null:
        return

    var find_grid_node: Callable = func (coordinates: Vector3i) -> GridNode:
        var idx: int = panel.all_level_nodes.find_custom(
            func (node: GridNode) -> bool:
                return node.coordinates == coordinates
        )

        if idx == -1:
            return null

        return panel.all_level_nodes[idx]

    for entity: GridEntity in panel.level.find_children("", "GridEntity", true, false):
        print_debug("[GLD Level Actions] Positioning %s" % entity)
        if entity._spawn_node != null:
            var spawn_anchor: GridAnchor = GridNode.find_grid_anchor(
                entity._spawn_node,
                entity._spawn_anchor_direction,
                find_grid_node,
            )

            if spawn_anchor != null:
                entity.global_position = spawn_anchor.global_position
            else:
                if entity._spawn_node != null:
                    entity.global_position = GridNode.get_center_pos(entity._spawn_node, level)
        GridEntity.orient(entity)

        unsaved = true

    if unsaved:
        EditorInterface.mark_scene_as_unsaved()

func _on_infer_spawn_of_entities_pressed() -> void:
    var level: GridLevelCore = panel.level
    for entity: GridEntity in panel.level.find_children("", "GridEntity", true, false):
        var coordinates: Vector3i = GridLevelCore.node_coordinates_from_position(level, entity, true)
        var idx: int = panel.all_level_nodes.find_custom(func (grid_node: GridNode) -> bool: return grid_node.coordinates == coordinates)
        if idx < 0:
            entity._spawn_node = null
            entity._spawn_anchor_direction = CardinalDirections.CardinalDirection.NONE
        else:
            entity._spawn_node = panel.all_level_nodes[idx]

    EditorInterface.mark_scene_as_unsaved()
